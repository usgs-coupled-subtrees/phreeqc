// --- include script file section ---
#include "ModifyPath.rul"

prototype ModifyBatchFile();


///////////////////////////////////////////////////////////////////////////////
//  EVENT:      Installed event is sent just after files for the component: 
//              Program Files are transferred. 
///////////////////////////////////////////////////////////////////////////////
 
export prototype ProgramFiles_Installed();
function ProgramFiles_Installed()
begin
    
    // modify path if nec
    // NOTE g_bModifyPath = FALSE when MAINTENANCE = TRUE        
    if (g_bModifyPath = TRUE) then
        g_bPATHModified = AddPath(TARGETDIR);
        if (g_bPATHModified) then
            // See BUG FIX(InstallShield Professional 6.31): below and in OnMoving
            RegDBSetAppInfo( "PATHModified", REGDB_STRING, "1", -1 );
        else
            // MessageBox("Unable to modify path", INFORMATION);
        endif;     
    endif;
    
    //{{ BUG FIX(InstallShield Professional 6.31): for Windows 95    
    if (MAINTENANCE && SYSINFO.WIN9X.bWin95 && g_bPATHModified) then
        RegDBSetAppInfo( "PATHModified", REGDB_STRING, "1", -1 );
    endif;
    //}} BUG FIX(InstallShield Professional 6.31): for Windows 95    
    
    // modify phreeqc.bat
    // Note: File Group "batch-file" is set to always overwrite
    // so that phreeqc.bat should be modified regardless of MAINTENANCE
    ModifyBatchFile();    
end;


///////////////////////////////////////////////////////////////////////////////
//  EVENT:      Uninstalling event is sent just before files for the component:
//              Program Files are removed during uninstallation. 
///////////////////////////////////////////////////////////////////////////////
 
export prototype ProgramFiles_UnInstalling();
function ProgramFiles_UnInstalling()
    BOOL bPathRemoved;
begin
    if (g_bPATHModified = TRUE) then
        bPathRemoved = RemovePath(TARGETDIR);
    endif;    
end;

///////////////////////////////////////////////////////////////////////////////
//
//  Function: ModifyBatchFile
//
//  Purpose: Modifies phreeqc.bat to be in sync with the installation
//           directory
//
//           Adds the following to the beginning of the batch file
//               @echo off
//               SET TD=C:\PROGRA~1\USGS\PHREEQC2.2
//               if "%TD%"=="C:\PROGRA~1\USGS\PHREEQC2.2" goto SET_OK_0
//
//           Adds the following to the end of the batch file
//               SET TD=
//
///////////////////////////////////////////////////////////////////////////////
function ModifyBatchFile()
    STRING szPhreeqcBatchFile;
    STRING szShortPath;
    STRING szAddString;
    STRING svOldBatchFile;
    #define BATCH_FILE TARGETDIR ^ "phreeqc.bat"
begin
    // Convert long path to short path therefore call from _Uninstalling rather than _Uninstalled
    szShortPath = TARGETDIR;
    LongPathToShortPath (szShortPath);

    // save default batch file
    BatchGetFileName (svOldBatchFile);

    // set default batch file
    BatchSetFileName (BATCH_FILE);

    // Add line 3
    szAddString = "if \"%TD%\"==\"" + szShortPath + "\" goto SET_OK_0";
    EzBatchAddString (szAddString, "", BEFORE|NOSET);

    // Add line 2
    szAddString = "TD=" + szShortPath;
    EzBatchAddString (szAddString, "", BEFORE);

    // Add line 1
    szAddString = "@echo off";
    EzBatchAddString (szAddString, "", BEFORE|NOSET);

    // Add to end of batch file
    EzBatchAddString ("TD=", "", AFTER);

    // Reset to default batch file
    BatchSetFileName (svOldBatchFile);
end;
